# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=aws-sdk-cpp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.8.55
pkgrel=1
pkgdesc="AWS SDK for C++ (mingw-w64)"
arch=('any')
url="https://github.com/aws/aws-sdk-cpp"
license=('Apache-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
options=('strip' 'staticlibs')
source=("${_realname}-${pkgver}.tar.gz::https://github.com/aws/aws-sdk-cpp/archive/${pkgver}.tar.gz"
        "aws-sdk-cpp-pr-1333.patch"
        "WinHttp-missing-flags.patch"
        "Patch-cmake-submodules.patch"
        "BuildAwsCCommon.patch"        
        "BuildAwsChecksums.patch"
        "vista.patch")
sha256sums=('6295e05b44d365e15735f28cd003a01913e302bcd3ba659ef26759e650f412e9'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/aws-sdk-cpp-pr-1333.patch"
  patch -p1 -i "${srcdir}/WinHttp-missing-flags.patch"
  patch -p1 -i "${srcdir}/Patch-cmake-submodules.patch"
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  export CFLAGS="-D_WIN32_WINNT=0x0600"
  export CXXFLAGS="-D_WIN32_WINNT=0x0600"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -GNinja \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_SHARED_LIBS=OFF \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DBUILD_ONLY="config;s3;transfer;identity-management;sts" \
      -DENABLE_UNITY_BUILD=ON \
      -DAUTORUN_UNIT_TESTS=OFF \
      ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}"/build-${CARCH}
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --build . --target install
}

check() {
  cd "${srcdir}"/build-${CARCH}
  #./aws-cpp-sdk-core-tests/aws-cpp-sdk-core-tests.exe
}
